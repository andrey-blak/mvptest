buildscript {
    repositories {
        mavenCentral ()
    }
    dependencies {
        classpath group: 'me.tatarka', name: 'gradle-retrolambda', version: '3.2.5'
        classpath group: 'com.neenbedankt.gradle.plugins', name: 'android-apt', version: '1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral ()
    jcenter ()
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId 'blak.mvx'
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner "blak.mvx.TestRunner"
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile ('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile ('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile ('proguard-android.txt'), 'proguard-test-rules.txt'
        }
    }
}

dependencies {
    compile fileTree (dir: 'libs', include: ['*.jar'])

    // support
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'

    // android common
    compile 'com.jakewharton:butterknife:7.0.1'

    // log
    compile group: 'com.github.tony19', name: 'logback-android-classic', version: '1.1.1-3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'

    // Rest
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.google.code.gson:gson:2.6.2'

    // Rx
    compile group: 'io.reactivex', name: 'rxjava', version: '1.1.0'
    compile group: 'io.reactivex', name: 'rxandroid', version: '1.1.0'

    // Dagger 2
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Test
    testApt 'com.google.dagger:dagger-compiler:2.0.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.4'
    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

    androidTestApt 'com.google.dagger:dagger-compiler:2.0.2'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}
